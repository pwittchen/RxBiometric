// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'io.codearte.nexus-staging'

ext {
  minSdkVersion = 14
  compileSdkVersion = 28
  buildToolsVersion = '28.0.2'
  gradleVersion = '4.4.1'
  kotlinVersion = '1.3.72'
  detektVersion = '1.0.0.RC8'
}

ext.deps = [
              rxjava2             : 'io.reactivex.rxjava2:rxjava:2.2.19',
              rxandroid2          : 'io.reactivex.rxjava2:rxandroid:2.1.1',
              rxkotlin2           : 'io.reactivex.rxjava2:rxkotlin:2.3.0',
              annotations         : 'androidx.annotation:annotation:1.1.0',
              appcompat           : 'androidx.appcompat:appcompat:1.1.0',
              material            : 'com.google.android.material:material:1.3.0-alpha01',
              design              : 'com.android.support:design:28.0.0',
              constraintlayout    : 'androidx.constraintlayout:constraintlayout:2.0.0-beta8',
              junit               : 'junit:junit:4.13',
              truth               : 'com.google.truth:truth:1.0.1',
              robolectric         : 'org.robolectric:robolectric:4.3.1',
              mockitocore         : 'org.mockito:mockito-core:3.3.3',
              dokka               : 'org.jetbrains.dokka:dokka-gradle-plugin:0.11.0-dev-59',
              detekt              : "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion",
              kotlinx             : "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion",
              kotlinstdlib        : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion",
              kotlingradleplugin  : "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
              biometrics          : "androidx.biometric:biometric:1.0.1"]

buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.0'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

nexusStaging {
  packageGroup = GROUP //optional if packageGroup == project.getGroup()
  stagingProfileId = "9add401d06ecc9" //when not defined will be got from server using "packageGroup"
  username = getRepositoryUsername()
  password = getRepositoryPassword()
}

task clean(type: Delete) {
  delete rootProject.buildDir
}